PREFIX apf: <http://jena.apache.org/ARQ/property#>
PREFIX dwec: <https://dwec.data.world/v0/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT
    ?columnTypeName
    ?columnSize
    ?columnIndex
    ?columnIsNullable
    ?type_prefix  # NOT USED BUT INCLUDED IN RESULT SET FOR NOW
    ?databaseIRI
    ?database_name
    ?table_name
    ?schema
    ?type
    ?column_name
    ?column_IRI
    ?description
    ?business_summary
    ?restricted_to_public_disclosure_per_federal_or_state_law
    ?sensitive_data
    ?data_ownner
    ?data_steward
    ?technical_steward
    ?status
    ?isPrimaryKey

WHERE
{
    ?column_IRI dwec:columnTypeName ?columnTypeName .

    ?typeRaw rdfs:subClassOf* dwec:DatabaseColumn .

    ?column_IRI a ?typeRaw ;
    dct:isPartOf ?table_IRI ;
    dct:identifier ?column_name .

    OPTIONAL {
        ?column_IRI dct:isPartOf ?table_IRI .
        ?table_IRI orgprofile:pk ?isPrimaryKey .
    }

    OPTIONAL {
        ?column_IRI dct:isPartOf ?table_IRI .
        ?table_IRI rdf:type ?table_rdf_type .
        ?table_rdf_type apf:splitIRI (?table_type_prefix  ?table_type) .
    }

    FILTER (?table_type IN ( "DatabaseTable" ) ) .

    ?table_IRI dct:isPartOf ?databaseIRI ;
    dct:identifier ?table_name .

    OPTIONAL {
        ?table_IRI dct:isPartOf ?schemaIRI .

        ?schemaIRI a dwec:DatabaseSchema ;
        dct:identifier ?schema .
    }

    ?typeRaw apf:splitIRI (?type_prefix  ?type) .

    ?columnCatalogRecord foaf:primaryTopic ?column_IRI .

    ?databaseIRI dct:identifier ?database_name ;
         a dwec:Database .

    #columnSize
    OPTIONAL {
        ?column_IRI dwec:columnSize ?columnSize .
    }
    #columnIndex
    OPTIONAL {
        ?column_IRI dwec:columnIndex ?columnIndex .
    }
    #columnIsNullable
    OPTIONAL {
        ?column_IRI dwec:columnIsNullable ?columnIsNullable .
    }
    #description
    OPTIONAL {
        ?column_IRI dct:description ?description .
    }
    # Program Office
    OPTIONAL {
        ?column_IRI :program_office ?program_office .
    }
    # Data Ownner
    OPTIONAL {
        ?column_IRI :data_ownner ?data_ownner .
    }
    # Data Steward
    OPTIONAL {
        ?column_IRI :data_steward ?data_steward .
    }
    # Technical Steward
    OPTIONAL {
        ?column_IRI :technical_steward ?technical_steward .
    }
    # Business Summary
    OPTIONAL {
        ?column_IRI :business_summary ?business_summary .
    }
    # Restricted to Public Disclosure
    OPTIONAL {
        ?column_IRI :restricted_to_public_disclosure ?restricted_to_public_disclosure_per_federal_or_state_law_label .
        ?restricted_to_public_disclosure_per_federal_or_state_law_label skos:altLabel ?restricted_to_public_disclosure_per_federal_or_state_law
    }
    # Sensitive Data
    OPTIONAL {
        ?column_IRI :sensitive_data ?sensitive_data_label .
        ?sensitive_data_label skos:altLabel ?sensitive_data .
    }
    # Status
    OPTIONAL {
        ?column_IRI dwec:hasAssetStatus ?status_label .
        ?status_label rdfs:label ?status .
    }
}
GROUP BY
    ?columnTypeName
    ?columnSize
    ?columnIndex
    ?columnIsNullable
    ?type_prefix  # NOT USED BUT INCLUDED IN RESULT SET FOR NOW
    ?databaseIRI
    ?database_name
    ?table_name
    ?schema
    ?type
    ?column_name
    ?column_IRI
    ?description
    ?business_summary
    ?restricted_to_public_disclosure_per_federal_or_state_law
    ?sensitive_data
    ?data_ownner
    ?data_steward
    ?technical_steward
    ?status
    ?isPrimaryKey
